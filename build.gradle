buildscript {
    ext {
        projectGroupName = 'fr.lacombe.pomodorover'
        projectVersion = '0.0.1-SNAPSHOT'

        projectSourceCompatibility = JavaVersion.VERSION_11
        projectTargetCompatibility = JavaVersion.VERSION_11

        // Dependencies Versions \\

        // Spring Boot
        springBootVersion = '2.1.0.RELEASE'

        //MongoDB
        mongoDBVersion = '3.4.3'

        // Tests
        junitVersion = '5.3.1'
        assertjVersion = '3.11.1'
        mockitoVersion = '2.23.0'
    }

    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group projectGroupName
    version projectVersion

    sourceCompatibility = projectSourceCompatibility
    targetCompatibility = projectTargetCompatibility

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }

    afterEvaluate {

        compileJava {
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                ]
                classpath = files()
            }
        }

        compileTestJava {
            inputs.property("moduleName", moduleName)
            doFirst {
                options.compilerArgs = [
                        '--module-path', classpath.asPath,
                        '--add-modules', 'org.junit.jupiter.api',
                        '--add-modules', 'org.junit.platform.commons',
                        '--add-modules', 'org.junit.platform.engine',
                        '--add-modules', 'org.junit.jupiter.params',
                        '--add-modules', 'org.junit.jupiter.engine',
                        '--add-modules', 'org.assertj.core',
                        '--add-modules', 'org.mockito',
                        '--add-modules', 'objenesis',
                        '--add-modules', 'mockito.junit.jupiter',
                        '--add-modules', 'net.bytebuddy',
                        '--add-modules', 'net.bytebuddy.agent',
                        '--add-reads', "$moduleName=org.junit.jupiter.api",
                        '--add-reads', "$moduleName=org.mockito",
                        '--add-reads', "$moduleName=org.junit.platform.commons",
                        '--add-reads', "$moduleName=org.junit.platform.engine",
                        '--add-reads', "$moduleName=org.junit.jupiter.params",
                        '--add-reads', "$moduleName=org.junit.jupiter.engine",
                        '--add-reads', "$moduleName=objenesis",
                        '--add-reads', "$moduleName=mockito.junit.jupiter",
                        '--add-reads', "$moduleName=net.bytebuddy",
                        '--add-reads', "$moduleName=net.bytebuddy.agent",
                        '--add-reads', "$moduleName=org.assertj.core",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.srcDirs).asPath,
                ]
                classpath = files()
            }
        }

        test {
            inputs.property("moduleName", moduleName)
            doFirst {
                jvmArgs = [
                        '-noverify',
                        '--module-path', classpath.asPath,
                        '--add-modules', 'ALL-MODULE-PATH',
                        '--add-reads', "$moduleName=org.junit.jupiter.api",
                        '--add-reads', "$moduleName=org.mockito",
                        '--add-reads', "$moduleName=org.junit.jupiter.params",
                        '--add-reads', "$moduleName=org.junit.jupiter.engine",
                        '--add-reads', "$moduleName=org.junit.platform.engine",
                        '--add-reads', "$moduleName=org.junit.platform.commons",
                        '--add-reads', "$moduleName=mockito.junit.jupiter",
                        '--add-reads', "$moduleName=objenesis",
                        '--add-reads', "$moduleName=net.bytebuddy",
                        '--add-reads', "$moduleName=net.bytebuddy.agent",
                        '--add-reads', "$moduleName=org.assertj.core",
                        '--patch-module', "$moduleName=" + files(sourceSets.test.java.outputDir).asPath,
                ]
                classpath = files()
            }
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
        testImplementation "org.assertj:assertj-core:${assertjVersion}"
        testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    }
}

// https://guides.gradle.org/building-java-9-modules/